plugins {
	id 'java'
	id 'groovy'
	id 'eclipse'
	id 'maven'
	id 'maven-publish'
	id 'signing'
	id 'com.jfrog.bintray' version '1.0'
}

group = 'org.unbroken-dome.gradle-plugins'
archivesBaseName = 'gradle-testsets-plugin'
version = '1.0.0'


sourceCompatibility = '1.8'
targetCompatibility = '1.8'


repositories {
	mavenCentral()
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}


dependencies {
	compile gradleApi()
	
	compile 'com.google.guava:guava:18.0'
	
	testCompile 'org.codehaus.groovy:groovy-all:2.3.6'
	testCompile 'org.spockframework:spock-core:1.0-groovy-2.3-SNAPSHOT'
	testCompile 'org.hamcrest:hamcrest-all:1.3'
}

configurations {
	all*.exclude module: 'junit-dep'
}


task javadocJar(type: Jar) {
	description = 'Assembles a jar archive containing the javadocs.'
	classifier = 'javadoc'
	from javadoc
}


task sourcesJar(type: Jar) {
	description = 'Assembles a jar archive containing the sources.'
	classifier = 'sources'
	from sourceSets.main.allSource
}


artifacts {
	archives javadocJar, sourcesJar
}


signing {
	sign configurations.archives
}


eclipse.project {
	natures 'org.springsource.ide.eclipse.gradle.core.nature'
}


def pomDefinition = pom {
	project {
		name project.friendly_name
		packaging 'jar'
		description project.description
		url project.home_url
		
		scm {
			connection "scm:git:${project.scm_url}"
			developerConnection "scm:git:${project.scm_url}"
			url project.scm_url
		}
		
		licenses {
			license {
				name = project.license_name
				url project.license_url
			}
		}
		
		developers {
			developer {
				name 'Till Krullmann'
				email 'till.krullmann@gmx.net'
			}
		}
	}
}


install {
	repositories.mavenInstaller {
		beforeDeployment { deployment -> signing.signPom(deployment) }
		
		pom = pomDefinition
	}
}


uploadArchives {
	repositories.mavenDeployer {
		beforeDeployment { deployment -> signing.signPom(deployment) }
		
		repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
			authentication(userName: ossrh_user, password: ossrh_password)
		}
		
		snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
			authentication(userName: ossrh_user, password: ossrh_password)
		}
		
		pom = pomDefinition
	}
}


bintray {
	user = bintray_user
	key = bintray_key
	configurations = ['archives']
	
	pkg {
		repo = project.bintray_repo
		name = project.name
		desc = project.description
		websiteUrl = project.home_url
		licenses = ['MIT']
		labels = ['gradle', 'plugin', 'test', 'testset']
		
		vcsUrl = project.scm_url
		issueTrackerUrl = project.issues_url
		publicDownloadNumbers = true
		
		version {
			vcsTag = project.version
			attributes = [
				'gradle-plugin': "org.unbroken-dome.test-sets:${project.group}:${project.name}"
			]
		}
	}
}
